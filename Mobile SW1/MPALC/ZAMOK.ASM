        LIST P=16C84,E=2
;CONST
INTCON		EQU	0Bh
STATUS		EQU	03h
OPTN		EQU	01h	;(81h)
TRISA		EQU	05h	;(85h)
TRISB		EQU	06h	;(86h)
EECON1		EQU	08h	;(88h)
EECON2		EQU	09h	;(89h)
EEADR		EQU	09h
EEDATA		EQU	08h

PORTA		EQU	05h
PORTB		EQU	06h

;RAM
FLAGS	        EQU     0Eh
KEYFL		EQU	0	;BITS
KEYFL1		EQU	1	;
ALERT		EQU	2	;IF ALERT=1 THEN SIGNALIZATION ACTIVAITED
EEPROG		EQU	3	;MODE OF WRITE NEW COD
KEYERR		EQU	4	;IF 1 THEN PRESS ERROR KEY
ALERTF		EQU	5	;WAIT FOR ACTIVAITED ALERT (FIRST WDT)

SCAN	        EQU     0Fh
SCAN1	        EQU     10h
SCANCNT         EQU     11h
TIME		EQU	12h     ;FOR TM
TIME1		EQU	13h	;FOR TMLONG

KEYTMR		EQU	14h
KEYTMR1		EQU	15h
KEYCNT		EQU	16h	;QWANTITI OF PRESS KEY
DAT		EQU	17h	;DATA FOR EEPROM
ADDR		EQU	18h	;ADRESS	FOR EEPROM
;Delayreg1       equ     18h
;Delayreg2       equ     19h
;Doorf           equ     20h
; ячейки ПЗУ
;EE_Nkey         equ     00h
; биты порта А
;DOORD           equ     0       ; датчик двери
;TRANZ           equ     1       ; силовой транзистор
;RED_LED         equ     2       ; светодиод
;RELE            equ     3       ; реле направления
;ALERT           equ     4       ; сигнализация взлома
; биты Doorf
;ZAMOK           equ     1
;DVER            equ     0
; Рабочая секция : начало исполняемого кода

        ORG     0
	CLRF	FLAGS
        goto    MAIN

        ORG     30h
MAIN	CALL	INIP
	BSF	PORTA,3
	CALL	TM
	BTFSS	FLAGS,ALERT
	BCF	PORTA,3



BEGIN				;CLRF	PORTA
	MOVLW	B'00001110'
	MOVWF	PORTB
	CALL	KEYPRESS
	BTFSC	FLAGS,KEYFL
	GOTO	PRESS
	MOVLW	B'00001101'
	MOVWF	PORTB
	CALL	KEYPRESS
	BTFSC	FLAGS,KEYFL
	GOTO	PRESS
	MOVLW	B'00001011'
	MOVWF	PORTB
	CALL	KEYPRESS
	BTFSC	FLAGS,KEYFL
	GOTO	PRESS
	MOVLW	B'00000111'
	MOVWF	PORTB
	CALL	KEYPRESS
	BTFSC	FLAGS,KEYFL
	GOTO	PRESS

	CALL	ALERTC
	CLRF	PORTB

	MOVLW	1		;IF EE_PROG THEN NO SLEEP
	BTFSC	FLAGS,EEPROG
	MOVWF	KEYTMR1
	CLRWDT


	MOVLW	B'11111111'
	ANDWF	KEYTMR,0
	BZ	TMR3
	GOTO	TMR4
TMR3	MOVLW	B'11111111'
	ANDWF	KEYTMR1,0
	BZ	SL1
	DECF	KEYTMR1,1
	MOVLW	0FFH
	MOVWF	KEYTMR
	GOTO	BEGIN
TMR4	DECF	KEYTMR,1
	GOTO	BEGIN

SL1	MOVLW	B'00010000'
	MOVWF	PORTA

	SLEEP
	BSF	INTCON,3
	BCF	INTCON,0
	GOTO	MAIN		

PRESS	CALL	ALERTC
	MOVLW	0ffh
	MOVWF	KEYTMR
	MOVLW	1h
	MOVWF	KEYTMR1
	BTFSC	FLAGS,KEYFL1
	GOTO	CON1
	MOVLW	01h
	MOVWF	SCANCNT
	BSF	FLAGS,KEYFL1
	MOVFW	SCAN
	MOVWF	SCAN1
	GOTO	BEGIN

CON1	MOVFW	SCAN1
	XORWF	SCAN,0
	BZ	CON2
	BCF	FLAGS,KEYFL1
	GOTO	BEGIN
CON2	DECFSZ	SCANCNT,1
	GOTO	BEGIN
	CALL	DECOD
	CLRWDT
	CLRF	PORTB
	BCF	FLAGS,KEYFL1
	CLRWDT
	MOVLW	0FFH
	MOVWF	KEYTMR
	MOVLW	1h
	MOVWF	KEYTMR1
CON3	CALL	ALERTC
	CLRWDT
	CALL	KEYPRESS
	BTFSC	FLAGS,KEYFL
	GOTO	CON4		
	DECFSZ	SCANCNT,1
	GOTO	CON3
	GOTO	BEGIN
CON4	MOVLW	0fh
	MOVWF	SCANCNT
	GOTO	CON3



INIP    CLRWDT
	BCF	FLAGS,KEYERR
	BTFSC	PORTA,4
	BCF	FLAGS,ALERTF
	MOVLW	0Ch
	MOVWF	KEYTMR
	CLRF	KEYTMR1
	CLRF	KEYCNT
	CLRF	PORTA
        CLRF    PORTB
	CLRF	INTCON
	BSF	INTCON,3    
	BSF	INTCON,6
	BSF     STATUS,5
        MOVLW   B'00010000'
        MOVWF   TRISA
        MOVLW   B'11110000'
        MOVWF   TRISB
        BSF     OPTN,0
        BSF     OPTN,1
        BSF     OPTN,2
        BCF     OPTN,3
        BCF     OPTN,5         ;INTERNAL GENERATOR
        BCF     OPTN,7         ;RESISTORS WITH Vdd
        BCF	STATUS,5
	CLRWDT
	RETURN

TM      MOVLW   0fh
        MOVWF	TIME
TMCON	CLRWDT
	DECFSZ	TIME,1
	GOTO	TMCON
	RETURN

TML	MOVLW   0EFh
        MOVWF	TIME1
TMLCON	CALL	TM
	DECFSZ	TIME1,1
	GOTO	TMLCON
	RETURN

KEYPRESS
	CLRWDT
	MOVFW	PORTB
	MOVWF	SCAN
	ANDLW	B'11110000'
	XORLW	B'11110000'
	BZ	NOTP
	BSF	FLAGS,KEYFL
	RETURN
NOTP	BCF	FLAGS,KEYFL
	RETURN

DECOD	MOVLW	B'00001000'
	MOVWF	PORTA
	CALL	TM
	CLRF	PORTA

	MOVFW	SCAN
	XORLW	B'11101110'
	BZ	PROG1

	MOVFW	SCAN
	XORLW	B'11101101'
	BZ	ALERT1

	MOVFW	SCAN
	XORLW	B'11101011'
	BZ	OPEN

	MOVFW	SCAN
	XORLW	B'11100111'
;	BZ	CLOSE

	MOVFW	SCAN
	XORLW	B'11011110'
	BZ	DIG1

	MOVFW	SCAN
	XORLW	B'11011101'
	BZ	DIG2

	MOVFW	SCAN
	XORLW	B'11011011'
	BZ	DIG3

	MOVFW	SCAN
	XORLW	B'11010111'
	BZ	DIG4

	MOVFW	SCAN
	XORLW	B'10111110'
	BZ	DIG5

	MOVFW	SCAN
	XORLW	B'10111101'
	BZ	DIG6

	MOVFW	SCAN
	XORLW	B'10111011'
	BZ	DIG7

	MOVFW	SCAN
	XORLW	B'10110111'
	BZ	DIG8

	MOVFW	SCAN
	XORLW	B'01111110'
	BZ	DIG9

	MOVFW	SCAN
	XORLW	B'01111101'
	BZ	DIG0

	MOVFW	SCAN
	XORLW	B'01111011'
	BZ	DIGA

	MOVFW	SCAN
	XORLW	B'01110111'
	BZ	DIGB
	RETURN

PROG1	BTFSC	FLAGS,EEPROG
	GOTO	PROG2
	BSF	FLAGS,EEPROG
	BCF	FLAGS,ALERT
	BCF	FLAGS,KEYERR	
	CLRF	KEYCNT
	RETURN	
PROG2	BCF	FLAGS,EEPROG
	MOVLW	3Fh
	MOVWF	EEADR
	CLRW
	XORWF	KEYCNT,0
	BZ	EXITPR
	DECF	KEYCNT,1
	MOVFW	KEYCNT
	MOVWF	EEDATA
	CALL	EEWRITE	
	CLRF	KEYCNT
EXITPR	RETURN

ALERT1	BTFSC	FLAGS,ALERT
	GOTO	RALERT
	BSF	FLAGS,ALERT
	BSF	FLAGS,ALERTF
	RETURN
RALERT	BCF	FLAGS,ALERT
	RETURN

DIG1	MOVLW	1
	MOVWF	DAT
	GOTO	DIGCON
DIG2	MOVLW	2
	MOVWF	DAT
	GOTO	DIGCON
DIG3	MOVLW	3
	MOVWF	DAT
	GOTO	DIGCON
DIG4	MOVLW	4
	MOVWF	DAT
	GOTO	DIGCON
DIG5	MOVLW	5
	MOVWF	DAT
	GOTO	DIGCON
DIG6	MOVLW	6
	MOVWF	DAT
	GOTO	DIGCON
DIG7	MOVLW	7
	MOVWF	DAT
	GOTO	DIGCON
DIG8	MOVLW	8
	MOVWF	DAT
	GOTO	DIGCON
DIG9	MOVLW	9
	MOVWF	DAT
	GOTO	DIGCON
DIG0	MOVLW	0
	MOVWF	DAT
	GOTO	DIGCON
DIGA	MOVLW	0Ah
	MOVWF	DAT
	GOTO	DIGCON
DIGB	MOVLW	0Bh
	MOVWF	DAT
	GOTO	DIGCON


DIGCON	BTFSS	FLAGS,KEYERR	;!
	GOTO	DIGCON1
	CLRF	KEYCNT
;	MOVLW	8
;	MOVWF	TIME
;	CALL	TMCON
	RETURN	

DIGCON1	BTFSC	FLAGS,EEPROG
	GOTO	PROGKEY
	
	MOVFW	KEYCNT
	MOVWF	EEADR
	CALL	EEREAD
	MOVFW	EEDATA
	XORWF	DAT,0
	BZ	KEY_OK
	BSF	FLAGS,KEYERR
;	CALL	BEEP
	CLRF	KEYCNT
	RETURN
KEY_OK	CLRWDT
	MOVLW	3Fh
	MOVWF	EEADR	
	CALL	EEREAD
	MOVFW	EEDATA
	XORWF	KEYCNT,0
	BZ	COD_OK
	INCF	KEYCNT
	RETURN
COD_OK	BTFSC	FLAGS,KEYERR
	RETURN
	BCF	FLAGS,ALERT
	CLRF	KEYCNT
	GOTO	OPEN

PROGKEY	MOVFW	KEYCNT
	MOVWF	EEADR
	MOVFW	DAT
	MOVWF	EEDATA
	CALL	EEWRITE
	INCF	KEYCNT
	RETURN

OPEN	BSF	PORTA,1
	CALL	TML
	BCF	PORTA,1
	RETURN

BEEP	BSF	PORTA,0
	MOVLW	0FFh
	MOVWF	TIME
	CALL	TMCON
	BCF	PORTA,0
	RETURN


ALERTC	BTFSS	FLAGS,ALERT
	RETURN
	BTFSC	FLAGS,ALERTF
	RETURN
	BTFSC	PORTA,4
	RETURN
	BSF	PORTA,2
	CALL	TML
	CALL	TML
	CALL	TML
	CALL	TML
	CALL	TML
	CALL	TML


	CALL	TML
	BCF	PORTA,2
	BCF	FLAGS,ALERT
	RETURN


; Эта подпpогpамма записывает в EEPROM
;
EEWRITE	BSF	STATUS,5
        BSF	EECON1,2
        MOVLW	055h
	MOVWF	EECON2
	MOVLW	0AAh
	MOVWF	EECON2
	BSF	EECON1,1
_EEWRI	BTFSS	EECON1,4
	GOTO	_EEWRI

	BCF	EECON1,2
	BCF	EECON1,4
	BCF	STATUS,5
;	MOVFW	EEDATA
;	MOVWF	DAT
;	CALL	EEREAD
;	MOVFW	EEDATA
;	XORWF	DAT,0
;	BZ	PR1
;	CALL	OPEN
PR1	RETURN

; Эта подпpогpамма считывает из EEPROM

EEREAD	BSF	STATUS,5h
	BSF	EECON1,0
        BCF	STATUS,5h
	RETURN

END